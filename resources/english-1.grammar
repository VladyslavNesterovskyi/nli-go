[
    rule: dp(D1) -> determiner(D1);
    rule: dp(D1) -> article(D1);
    rule: dp(D1) -> qp(D1);

    rule: qp(Q1) -> quantifier(Q1);
    rule: qp(D1) -> number(D1);
    rule: qp(Q1) -> qp(Q2) conjunction(Q1) qp(Q3),                             sense: conjunction(Q1, Q2, Q3);
    rule: qp(Q1) -> adjective(A1) subConjunction() number(N1),                 sense: relation(Q1, A1, N1);

    rule: adjective(A1) -> adjective(A2) conjunction(A1) adjective(A3),        sense: conjunction(A1, A2, A3);

    rule: adverb(A1) -> adverb(A2) conjunction(A1) adverb(A3),                 sense: conjunction(A1, A2, A3);

    rule: advp(A1) -> adverb(A1);
    rule: advp(A1) -> degree(D1) adverb(A1),                                   sense: degree(A1, D1);
    rule: advp(A1) -> advp(A2) conjunction(A1) advp(A3),                       sense: conjunction(A1, A2, A3);

    rule: adjp(A1) -> adjective(A1);
    rule: adjp(A1) -> degree(D1) adjective(A1),                                sense: degree(A1, D1);
    rule: adjp(A1) -> advp(ADV1) adjective(A1),                                sense: specification(A1, ADV1);
    rule: adjp(A1) -> adjp(A2) conjunction(A1) adjp(A3),                       sense: conjunction(A1, A2, A3);

    rule: properNoun(N1) -> fullName(N1);
    rule: properNoun(N1) -> firstName(N1) lastName(N1);
    rule: properNoun(N1) -> firstName(N1) insertion(N1) lastName(N1);

    rule: np(E1) -> nbar(E1);
    rule: np(E1) -> dp(D1) nbar(E1),                                           sense: quantification(E1, [], D1, []);
    rule: np(E1) -> poss(E2) nbar(E1),                                         sense: possession(E1, E2);
    rule: np(E1) -> pronoun(E1);
    rule: np(E1) -> properNoun(E1);
    rule: np(E1) -> sbar(E1);
    rule: np(E1) -> np(E2) conjunction(E1) np(E3),                             sense: conjunction(E1, E2, E3);

    rule: nbar(E1) -> noun(E1);
    rule: nbar(E1) -> adjp(A1) nbar(E1),                                       sense: specification(E1, A1);
    rule: nbar(E1) -> nbar(E1) pp(P),                                          sense: specification(E1, P);
    rule: nbar(E1) -> nbar(E1) sbar(S),                                        sense: specification(E1, S);

    rule: poss(E1) -> np(E1) possessiveMarker();

    rule: preposition(P1) -> preposition(P2) conjunction(P1) preposition(P3),  sense: conjunction(P1, P2, P3);

    rule: pp(R1) -> preposition(P1) np(E1),                                    sense: relation(R1, P1, E1);
    rule: pp(R1) -> preposition(P1) pp(P2),                                    sense: relation(R1, P1, P2);
    rule: pp(R1) -> pSpec(P1) preposition(P2) np(E1),                          sense: relation(R1, P2, E1) specification(R1, P1);
    rule: pp(R1) -> pp(P1) conjunction(R1) pp(P2),                             sense: conjunction(R1, P1, P2);

    rule: vgp(V1) -> verb(V1);
    rule: vgp(V1) -> verb(V1) particle(P1),                                    sense: modifier(V1, P1);
    rule: vgp(V1) -> modal(A1) verb(V1),                                       sense: modality(V1, A1);
    rule: vgp(V1) -> modal(A1) verb(V1) particle(P1),                          sense: modifier(V1, P1) modality(V1, A1);

    rule: vbar(V1) -> vgp(V1);
    rule: vbar(V1) -> vgp(V1) np(E1),                                          sense: object(V1, E1);
    rule: vbar(V1) -> vgp(V1) np(E1) np(E2),                                   sense: object(V1, E1) indirectObject(V1, E2);
    rule: vbar(V1) -> vgp(V1) np(E1) pp(P1),                                   sense: object(V1, E1) prepositionalObject(V1, P1);
    rule: vbar(V1) -> vgp(V1) advp(A1),                                        sense: specification(V1, A1);
    rule: vbar(V1) -> vgp(V1) np(E1) advp(A1),                                 sense: object(V1, E1) specification(V1, A1);
    rule: vbar(V1) -> vgp(V1) pp(P1),                                          sense: prepositionalObject(V1, P1);
    rule: vbar(V1) -> vgp(V1) pp(P1) pp(P2),                                   sense: prepositionalObject(V1, P1) prepositionalObject(V1, P2);
    rule: vbar(V1) -> vgp(V1) sbar(S),                                         sense: object(V1, S);
    rule: vbar(V1) -> vgp(V1) np(E1) particle(P),                              sense: modifier(V1, P) object(V1, E1);

    rule: vp(V1) -> vbar(V1);
    rule: vp(V1) -> vbar(V1) pp(P1),                                           sense: prepositionalObject(V1, P1);
    rule: vp(V1) -> vbar(V1) advp(A1),                                         sense: specification(V1, A1);
    rule: vp(V1) -> vbar(V1) np(E1),                                           sense: object(V1, E1);

    rule: sbar(S1) -> complementizer(C) clause(S1),                            sense: complement(S1, C);

    rule: clause(S1) -> np(E1) vp(S1),                                         sense: object(S1, E1);
    rule: clause(S1) -> np(E1) modal(M) vp(S1),                                sense: subject(S1, E1) modality(S1, M);
    rule: clause(S1) -> advp(A) clause(S1);
    rule: clause(S1) -> pp(P) clause(S1);
    rule: clause(S1) -> clause(S1) advp(A);
    rule: clause(S1) -> clause(S1) pp(P);
    rule: clause(S1) -> s(S1) sbar(S2),                                        sense: object(S1, S2);

    rule: sDeclarative(S1) -> clause(S1) period(),                             sense: declaration(S1);

    rule: sInterrogative(S1) -> auxVerb(A) np(E1) vp(S1) questionMark(),       sense: question(S1, yesNoQuestion) subject(S1, E1);
    rule: sInterrogative(S1) -> auxVerb(S1) np(E1) np(E2) questionMark(),      sense: question(S1, yesNoQuestion) subject(S1, E1) object(S1, E2);
    rule: sInterrogative(S1) -> whWord(W1) adjective(A1) nbar(E1) auxVerb(S1)
        np(E2) questionMark(),                                                 sense: question(S1, whQuestion) focus(E1) quantification(E1, [], A1, []) specifier(A1, W1) subject(S1, E1) object(S1, E2);
    rule: sInterrogative(S1) -> whWord(E1) vp(S1) np(E2) questionMark(),       sense: question(S1, whQuestion) focus(E1) subject(S1, E1) object(S1, E2);
    rule: sInterrogative(S1) -> whWord(D1) np(E1) auxVerb(S1)
        np(E2) questionMark(),                                                 sense: question(S1, whQuestion) focus(E1) quantification(E1, [], D1, []) subject(S1, E1) object(S1, E2);

    rule: sImperative(S1) -> vp(S1) exclamationMark(),                         sense: command(S1);

    rule: s(S1) -> sDeclarative(S1);
    rule: s(S1) -> sInterrogative(S1);
    rule: s(S1) -> sImperative(S1);
]