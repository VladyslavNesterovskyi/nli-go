[
    /* quantifiers */

    { rule: qp(E1) -> a(E1),                                               sense: number(E1, 1) }
    { rule: qp(E1) -> the(E1),                                             sense: the(E1) }
    { rule: qp(E1) -> at() least() one() of(),                             sense: some(E1) }

    /* verbs */

    { rule: tv(P1, E1, E2) -> pick() up(),                                 sense: pick_up(P1, E2) }
    { rule: tv(P1, E1, E2) -> grasp(),                                     sense: grasp(P1, E2) }
    { rule: tv(P1, E1, E2) -> find(),                                      sense: find(P1, E2) }
    { rule: tv_gerund(P1, E1, E2) -> holding(),                            sense: hold(P1, E1, E2) }

    { rule: vp(P1) -> tv(P1, _, E1) np(E1) }
    { rule: vp(P1) -> put() np(E1) into() np(E2),                          sense: put_into(P1, E1, E2) }
    { rule: vp(P1, E1) -> np(E2) to() tv(P1, _, E1) }

    { rule: vp_dep(P1, L1) -> np(P2) tell(P1) vp(P2, L1),                  sense: tell(P1, sem(3)) }

    /* nouns */

    { rule: adjp(E1) -> taller() than() np(E2),                            sense: taller(E1, E2) }
    { rule: adjp(E1) -> narrower() than() np(E2),                          sense: narrower(E1, E2) }
    { rule: adjp(E1) -> adjective(E1) }

    { rule: pp(E1) -> in() np(E2),                                         sense: do_contain(_, E2, E1) }

    { rule: relative_clause(E1) -> which() copula() adjp(E1) }
    { rule: relative_clause(E1) -> np(E2) aux_be() tv_gerund(P1, E2, E1) }
    { rule: relative_clause(E1) -> which() vp_dep(P1, E1) }

    { rule: np(E1) -> qp(Q1) nbar(E1),                                     sense: quant(Q1, sem(1), E1, sem(2), sem(parent)) }
    { rule: np(E1) -> nbar(E1),                                            sense: quant(_, some(_), E1, sem(1), sem(parent)) }
    { rule: np(E1) -> pronoun(E1),                                         sense: quant(_, all(_), E1, sem(1), sem(parent)) }

    { rule: nbar(E1) -> adjp(E1) nbar(E1) }
    { rule: nbar(E1) -> noun(E1) relative_clause(E1) }
    { rule: nbar(E1) -> noun(E1) }
    { rule: nbar(E1) -> them(E1) }

    /* Sentences */

    { rule: what_clause(P1, E2) -> aux_do() np(E1) contain(P1),                sense: do_contain(P1, E1, E2) }
    { rule: what_clause(P1, E2) -> aux_be() np(E1) support(P1) by(),           sense: support(P1, E2, E1) }

    { rule: how_many_clause(P1, E1) -> np(E1) copula() not() pp(E1),           sense: not(sem(4)) }

    { rule: interrogative_clause(P1) -> what() what_clause(P1, E1),             sense: what(E1) }
    { rule: interrogative_clause(P1) -> how() many() how_many_clause(P1, E1),   sense: how_many(E1) }
    { rule: interrogative_clause(P1) -> copula() np(E1) adjp(E1),               sense: select(E1) }

    { rule: interrogative(P1) -> interrogative_clause(P1) question_mark() }

    { rule: imperative_clause(P1) -> vp(P1),                                sense: command(P1) }
    { rule: imperative_clause(C) -> imperative_clause(P1) and(C) imperative_clause(P2),  sense: sequence(C, sem(1), sem(3)) }

    { rule: imperative(P1) -> imperative_clause(P1) period() }
    { rule: imperative(P1) -> imperative_clause(P1) }

    { rule: s(S1) -> imperative(S1) }
    { rule: s(S1) -> interrogative(S1),                                     sense: question(S1) }
]